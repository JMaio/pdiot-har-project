plugins {
    id('com.android.application')
    id('kotlin-android')
    id('kotlin-android-extensions')
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "io.github.jmaio.pdiotapp"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // https://www.tensorflow.org/lite/guide/android
        // We recommend most developers omit the x86, x86_64, and arm32 ABIs.
        // This can be achieved with the following Gradle configuration, which specifically
        // includes only armeabi-v7a and arm64-v8a, which should cover most modern Android devices.
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    aaptOptions {
        noCompress "tflite"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }

    // https://stackoverflow.com/a/56996020/9184658
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    // https://stackoverflow.com/a/44767574/9184658
    // https://stackoverflow.com/a/47509465/9184658
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude("META-INF/*.kotlin_module")
    }
}

// https://github.com/tensorflow/examples/blob/master/lite/examples/digit_classifier/android/app/build.gradle
project.ext.ASSET_DIR = projectDir.toString() + '/src/main/assets'

dependencies {
    // include local jars?
    implementation fileTree(dir: "libs", include: ["*.jar"])
    // local flask client
    implementation project(path: ':flaskServerClientTest')

    // android
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.1'
    // android jetpack
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"


    // bluetooth and dataviz
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"
    implementation 'com.neovisionaries:nv-bluetooth:1.8'
    implementation "com.polidea.rxandroidble2:rxandroidble:1.11.1"
    implementation 'com.github.doyaaaaaken:kotlin-csv-jvm:0.13.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9"

    // Google
    implementation 'com.google.android.gms:play-services-vision:20.1.2'
    implementation 'com.google.android.material:material:1.2.1'

    // tf lite
    implementation "org.tensorflow:tensorflow-lite:0.0.0-nightly"
    // guava: for evicting queue
    implementation 'com.google.guava:guava:30.0-jre'

    // unit testing
    testImplementation 'junit:junit:4.13.1'

    // android testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

